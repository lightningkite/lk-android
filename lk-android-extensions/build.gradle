//noinspection GradleCompatible
apply plugin: 'com.android.library'
apply plugin: 'kotlin-android'

group = "com.github.lightningkite"

android {
    compileSdkVersion 27
    //buildToolsVersion "27.0.2"

    defaultConfig {
        minSdkVersion 16
        targetSdkVersion 27
        versionCode 1
        versionName "1.0"
    }
    buildTypes {
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
        }
    }

    sourceSets {
        main.java.srcDirs += 'src/main/kotlin'
    }

    lintOptions {
        warning('InvalidPackage', 'NewApi')
    }
}

repositories {
    mavenCentral()
    maven {
        url "https://jitpack.io"
    }
    maven {
        url 'https://maven.google.com/'
        name 'Google'
    }
    jcenter()
}

dependencies {
    androidTestCompile 'com.android.support:support-annotations:27.0.2'
    androidTestCompile 'com.android.support.test:runner:1.0.1'
    testImplementation 'junit:junit:4.12'
    testImplementation "org.jetbrains.kotlin:kotlin-test-junit:${rootProject.kotlinVersion}"


    implementation fileTree(include: ['*.jar'], dir: 'libs')
    implementation "org.jetbrains.kotlin:kotlin-stdlib-jdk7:${rootProject.kotlinVersion}"

    implementation 'com.android.support:appcompat-v7:27.0.2'
    implementation 'com.android.support:recyclerview-v7:27.0.2'

//    implementation 'com.android.support:cardview-v7:27.0.2'
//    implementation 'com.android.support:gridlayout-v7:27.0.2'
//    implementation 'com.android.support:design:27.0.2'
}


//Bintray
buildscript {
    repositories {
        mavenCentral()
        jcenter()
    }
    dependencies {
        classpath 'digital.wup:android-maven-publish:3.0.0'
        classpath 'com.jfrog.bintray.gradle:gradle-bintray-plugin:1.8.0'
        classpath "org.jetbrains.kotlin:kotlin-gradle-plugin:${rootProject.kotlinVersion}"
    }
}

task sourceJar(type: Jar) {
    classifier 'sources'
    from android.sourceSets.main.java.srcDirs
}

apply plugin: 'digital.wup.android-maven-publish'
publishing {
    publications {
        mavenAar(MavenPublication){
            from components.android
            artifact tasks.sourceJar
            groupId rootProject.repositoryGroupName
            artifactId project.name
            version rootProject.libraryVersion
        }
    }
}


def bintrayPropertiesFile = rootProject.file("bintray.properties")
if(bintrayPropertiesFile.exists()) {
    def bintrayProperties = new Properties()
    bintrayProperties.load(new FileInputStream(bintrayPropertiesFile))
    apply plugin: 'com.jfrog.bintray'
    bintray {
        user = bintrayProperties['bintrayUser']
        key = bintrayProperties['bintrayKey']
        publications = ['mavenAar']
        publish = bintrayProperties['bintrayPublish']
        pkg {
            repo = rootProject.repositoryGroupName
            name = project.name
            userOrg = bintrayProperties['bintrayOrganization']
            licenses = [bintrayProperties['bintrayLicense']]
            vcsUrl = bintrayProperties['bintrayVcs']
            version {
                name = rootProject.libraryVersion
                released = new Date()
            }
        }
    }
}